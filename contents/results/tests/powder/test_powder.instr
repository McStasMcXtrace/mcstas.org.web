% Test of the powder source
%

DEFINE INSTRUMENT test_powder()

DECLARE
%{

/* Number of neutrons to send through. */
#define RUNNUM 100000
int run_num;

/* Global correction factor */
double MC_correction;

char *PSD_big_name = "testresults/powder_big.psd";
char *PSD_small_name = "testresults/powder_small.psd";
char *PSD_4pi_name = "testresults/powder_4pi.psd";

double d_phi0 = PI/40;
double scatter_angle = 70.0/180*PI;

/* Our main() function. */
int
main(int argc, char *argv[])
{
  mcreadparams();
  srandom(time(NULL));
  mcinit();
  while(run_num < RUNNUM)
  {
    mcsetstate(0, 0, 0, 0, 0, 1, 0, 0, 0, 1);
    mcraytrace();
    run_num++;
  }
  mcfinally();
  return 0;
}

%}

INITIALIZE
%{
  MC_correction = 1;  /* Scale factor is initially unity */
  run_num = 0;         /* Initialize mc neutron counter. */
%}

COMPONENT a1 = Axis()
  AT (0,0,0) ABSOLUTE

COMPONENT source = Source_aim(radius=0.05,
			  dist=1,
		          xw=0.12, yh=0.12, 
			  v0=1000, dv=0)
  AT (0,0,0) RELATIVE a1
  ROTATED (0,0,0) RELATIVE a1

COMPONENT target = Powder1(d_phi0 = EXTERN d_phi0, radius = 0.05, h = 0.1,
			   pack = 1, Vc = 100, sigma_a = 0.463,
			   q = 1.8049,
                           j = 6, DW = 1,
                           F2 = 10,
			   target_x = 1000,
			   target_y = 0, target_z = 1000)
  AT (0,0,1) RELATIVE a1 ROTATED (0,0,0) RELATIVE a1

COMPONENT a2 = Axis()
     AT (0,0,1) RELATIVE a1
     ROTATED (0,EXTERN scatter_angle, 0) RELATIVE a1

COMPONENT PSD_small = PSD_monitor(xmin=-0.2, xmax=0.2,
          ymin =-0.05, ymax=0.05, nx=100, ny=1,
         PSD_p=EXTERN PSD_small_p, PSD_N=EXTERN PSD_small_N,
         m1x=EXTERN PSD_small_m1x, m1y=EXTERN PSD_small_m1y,
         m2x=EXTERN PSD_small_m2x, m2y=EXTERN PSD_small_m2y,
	 filename=EXTERN PSD_small_name)
  AT (0,0,1) RELATIVE a2
  ROTATED (0,0,0) RELATIVE a2

COMPONENT PSD_big = PSD_monitor(xmin=-5, xmax=5,
          ymin =-4, ymax=4, nx=200, ny=160,
         PSD_p=EXTERN PSD_big_p, PSD_N=EXTERN PSD_big_N,
         m1x=EXTERN PSD_big_m1x, m1y=EXTERN PSD_big_m1y,
         m2x=EXTERN PSD_big_m2x, m2y=EXTERN PSD_big_m2y,
	 filename=EXTERN PSD_big_name)
  AT (0,0,2) RELATIVE a1
  ROTATED (0,0,0) RELATIVE a1

COMPONENT PSD_4pi = PSD_monitor_4PI(radius=5, nx=101, ny=51,
         PSD_p=EXTERN PSD_4pi_p, PSD_N=EXTERN PSD_4pi_N,
	 filename=EXTERN PSD_4pi_name)
  AT (0,0,1) RELATIVE a1 ROTATED (0,0,0) RELATIVE a1

FINALLY
%{
  printf("Finished simulation of %d neutrons.\n", run_num);
  printf("Correction factor: %g \n",MC_correction);
/*   printf("Moments: %g %g %g %g\n", PSD_m1x, PSD_m2x, PSD_m1y, PSD_m2y); */
%}

END
