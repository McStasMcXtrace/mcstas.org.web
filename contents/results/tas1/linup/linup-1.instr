DEFINE INSTRUMENT TAS1_2axis(PHM_d,TTM_d,C1,NCOUNT)

DECLARE
%{

/* Number of neutrons to send through. */
int ncount;

/* 30' mosaicity used on monochromator and analysator */
double tas1_mono_mosaic = 45*MIN2RAD;/* Measurements indicate its really 25' */
/* Q vector for bragg scattering with monochromator and analysator */
double tas1_mono_q = 2*1.87325*AA2MS;/* Fake 2nd order scattering for 20meV */
double tas1_mono_r0 = 0.6;

/* Collimators */
double coll1_div;

/* Angles in radians. */
double PHM_r, TTM_r;

double mpos0, mpos1, mpos2, mpos3, mpos4, mpos5, mpos6, mpos7;
double mrot0, mrot1, mrot2, mrot3, mrot4, mrot5, mrot6, mrot7;


/* Global correction factor */
double MC_correction;

char *vmon1_file_name = "sim/linup_1_1.vmon";

/* Our main() function. */
int
main(int argc, char *argv[])
{
  int run_num = 0;

  mcreadparams();
  mcinit();
  while(run_num < ncount)
  {
    mcsetstate(0, 0, 0, 0, 0, 1, 0, 0, 0, 1);
    mcraytrace();
    run_num++;
  }
  mcfinally();
  return 0;
}

%}

INITIALIZE
%{
  srandom(time(NULL));
  MC_correction = 1;  /* Scale factor is initially unity */
  coll1_div = C1*MIN2RAD;
  ncount = NCOUNT;
  {
    double d = 0.0125;		/* 12.5 mm between slab centers. */
    double phi = 0.0095;	/* Rotation between adjacent slabs. */
    mpos0 = -3.5*d; mrot0 = -3.5*phi;
    mpos1 = -2.5*d; mrot1 = -2.5*phi;
    mpos2 = -1.5*d; mrot2 = -1.5*phi;
    mpos3 = -0.5*d; mrot3 = -0.5*phi;
    mpos4 =  0.5*d; mrot4 =  0.5*phi;
    mpos5 =  1.5*d; mrot5 =  1.5*phi;
    mpos6 =  2.5*d; mrot6 =  2.5*phi;
    mpos7 =  3.5*d; mrot7 =  3.5*phi;
  }
  PHM_r = PHM_d*DEG2RAD;
  TTM_r = TTM_d*DEG2RAD;
%}

COMPONENT a1 = Axis()
  AT (0,0,0) ABSOLUTE

COMPONENT source = Source_aim(
	radius = 0.060,
	dist = 3.288,
	xw = 0.042, yh = 0.082, 
	v0 = 1955.8,		/* 20 meV */
	dv = 40)		/* Sufficient for TAS1 geometry */
  AT (0,0,0) RELATIVE a1 ROTATED (0,0,0) RELATIVE a1

COMPONENT slit1 = Slit(
	xmin=-0.020, xmax=0.065,
	ymin = -0.075, ymax = 0.075)
  AT (0, 0, 1.1215) RELATIVE a1 ROTATED (0,0,0) RELATIVE a1

COMPONENT slit2 = Slit(
	xmin = -0.020, xmax = 0.020,
	ymin = -0.040, ymax = 0.040)
  AT (0,0,1.900) RELATIVE a1 ROTATED (0,0,0) RELATIVE a1

COMPONENT slit3 = Slit(
	xmin = -0.021, xmax = 0.021,
	ymin = -0.041, ymax = 0.041)
  AT (0,0,3.288) RELATIVE a1 ROTATED (0,0,0) RELATIVE a1

COMPONENT focus_mono = Axis()
  AT (0, 0, 3.56) RELATIVE a1 ROTATED (0, EXTERN PHM_r, 0) RELATIVE a1

COMPONENT m0 = Monochromator(
	zmin=-0.0375,zmax=0.0375,ymin=-0.006,ymax=0.006,
	mosaich=EXTERN tas1_mono_mosaic,mosaicv=EXTERN tas1_mono_mosaic,
	r0=EXTERN tas1_mono_r0, Q=EXTERN tas1_mono_q)
  AT (0, EXTERN mpos0, 0) RELATIVE focus_mono
  ROTATED (0, 0, EXTERN mrot0) RELATIVE focus_mono

COMPONENT m1 = Monochromator(
	zmin=-0.0375,zmax=0.0375,ymin=-0.006,ymax=0.006,
	mosaich=EXTERN tas1_mono_mosaic,mosaicv=EXTERN tas1_mono_mosaic,
	r0=EXTERN tas1_mono_r0, Q=EXTERN tas1_mono_q)
  AT (0, EXTERN mpos1, 0) RELATIVE focus_mono
  ROTATED (0, 0, EXTERN mrot1) RELATIVE focus_mono

COMPONENT m2 = Monochromator(
	zmin=-0.0375,zmax=0.0375,ymin=-0.006,ymax=0.006,
	mosaich=EXTERN tas1_mono_mosaic,mosaicv=EXTERN tas1_mono_mosaic,
	r0=EXTERN tas1_mono_r0, Q=EXTERN tas1_mono_q)
  AT (0, EXTERN mpos2, 0) RELATIVE focus_mono
  ROTATED (0, 0, EXTERN mrot2) RELATIVE focus_mono

COMPONENT m3 = Monochromator(
	zmin=-0.0375,zmax=0.0375,ymin=-0.006,ymax=0.006,
	mosaich=EXTERN tas1_mono_mosaic,mosaicv=EXTERN tas1_mono_mosaic,
	r0=EXTERN tas1_mono_r0, Q=EXTERN tas1_mono_q)
  AT (0, EXTERN mpos3, 0) RELATIVE focus_mono
  ROTATED (0, 0, EXTERN mrot3) RELATIVE focus_mono

COMPONENT m4 = Monochromator(
	zmin=-0.0375,zmax=0.0375,ymin=-0.006,ymax=0.006,
	mosaich=EXTERN tas1_mono_mosaic,mosaicv=EXTERN tas1_mono_mosaic,
	r0=EXTERN tas1_mono_r0, Q=EXTERN tas1_mono_q)
  AT (0, EXTERN mpos4, 0) RELATIVE focus_mono
  ROTATED (0, 0, EXTERN mrot4) RELATIVE focus_mono

COMPONENT m5 = Monochromator(
	zmin=-0.0375,zmax=0.0375,ymin=-0.006,ymax=0.006,
	mosaich=EXTERN tas1_mono_mosaic,mosaicv=EXTERN tas1_mono_mosaic,
	r0=EXTERN tas1_mono_r0, Q=EXTERN tas1_mono_q)
  AT (0, EXTERN mpos5, 0) RELATIVE focus_mono
  ROTATED (0, 0, EXTERN mrot5) RELATIVE focus_mono

COMPONENT m6 = Monochromator(
	zmin=-0.0375,zmax=0.0375,ymin=-0.006,ymax=0.006,
	mosaich=EXTERN tas1_mono_mosaic,mosaicv=EXTERN tas1_mono_mosaic,
	r0=EXTERN tas1_mono_r0, Q=EXTERN tas1_mono_q)
  AT (0, EXTERN mpos6, 0) RELATIVE focus_mono
  ROTATED (0, 0, EXTERN mrot6) RELATIVE focus_mono

COMPONENT m7 = Monochromator(
	zmin=-0.0375,zmax=0.0375,ymin=-0.006,ymax=0.006,
	mosaich=EXTERN tas1_mono_mosaic,mosaicv=EXTERN tas1_mono_mosaic,
	r0=EXTERN tas1_mono_r0, Q=EXTERN tas1_mono_q)
  AT (0, EXTERN mpos7, 0) RELATIVE focus_mono
  ROTATED (0, 0, EXTERN mrot7) RELATIVE focus_mono

COMPONENT a2 = Axis()
  AT (0,0,0) RELATIVE focus_mono ROTATED (0, EXTERN TTM_r, 0) RELATIVE a1

COMPONENT slitMS1 = Slit(
	xmin = -0.0105, xmax = 0.0105, ymin = -0.035, ymax = 0.035)
  AT (0,0,0.565) RELATIVE a2 ROTATED (0,0,0) RELATIVE a2

COMPONENT slitMS2 = Slit(
	xmin = -0.0105, xmax = 0.0105, ymin = -0.035, ymax = 0.035)
  AT (0,0,0.855) RELATIVE a2 ROTATED (0,0,0) RELATIVE a2

COMPONENT c1 = Collimator_len(
	xmin = -0.02, xmax = 0.02, ymin = -0.0375, ymax = 0.0375,
	len = 0.250, divergence = EXTERN coll1_div)
  AT (0, 0, 0.87) RELATIVE a2 ROTATED (0,0,0) RELATIVE a2

COMPONENT slitMS3 = Circular_slit(radius = 0.025)
  AT (0,0,1.130) RELATIVE a2 ROTATED (0,0,0) RELATIVE a2

COMPONENT slitMS4 = Circular_slit(radius = 0.025)
  AT (0,0,1.180) RELATIVE a2 ROTATED (0,0,0) RELATIVE a2

COMPONENT slitMS5 = Circular_slit(radius = 0.0275)
  AT (0,0,1.230) RELATIVE a2 ROTATED (0,0,0) RELATIVE a2

COMPONENT vmon1 = v_monitor(
	xmin = -0.01, xmax = 0.01,
	ymin = -0.1, ymax = 0.1,
	vmin = 1920, vmax = 1990, n = 35,
	V_N = vmon1_N, V_p = vmon1_p, filename = EXTERN vmon1_file_name)
  AT(0, 0, 1.5) RELATIVE a2 ROTATED (0,0,0) RELATIVE a2

COMPONENT a3 = Axis()
  AT (0,0,1.565) RELATIVE a2 ROTATED (0, 0, 0) RELATIVE a2

COMPONENT slitS = Slit(
	xmin = -0.00525, xmax = 0.00525, ymin = -0.02025, ymax = 0.02025)
  AT (0,0,0) RELATIVE a3 ROTATED (0,0,0) RELATIVE a3

COMPONENT sng = Monitor(
	xmin = -0.025, xmax = 0.025, ymin = -0.0375, ymax = 0.0375,
	Nsum = EXTERN sng_Nsum, psum = EXTERN sng_psum)
  AT(0, 0, 0.020) RELATIVE a3 ROTATED (0,0,0) RELATIVE a3

FINALLY
%{
/*   printf("Finished simulation of %d neutrons.\n", ncount); */
  printf("Correction factor: %g \n",MC_correction);
  printf("I = %g (%d neutrons detected)\n", sng_psum/MC_correction, sng_Nsum);
%}

END
